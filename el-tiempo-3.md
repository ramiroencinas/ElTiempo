## Uso de m칩dulos, estructura y ciudades con sus coordenadas

En la parte anterior desarrollamos en `Raku` una funci칩n b치sica denominada `get-url` para conectar y descargar el c칩digo HTML de una web mediante SSL. Esta funci칩n vamos a utilizarla con la URL `https://open-meteo.com/en/docs`. Descargado el contenido, lo analizaremos para obtener la informaci칩n que relaciona las ciudades y sus coordenadas.

### Un m칩dulo para la funci칩n get-url

Un c칩digo f치cil de leer y de revisar separa el c칩digo principal de otras partes del c칩digo que realizan una tarea concreta m치s de una vez. Estas otras partes son funciones y su c칩digo va colocado en otros ficheros denominados librer칤as o `m칩dulos` en el caso de `Raku`. As칤, desde el c칩digo principal podemos cargar estos m칩dulos (librer칤as) y utilizarlos las veces necesarias despejando el c칩digo principal.

Para crear un nuevo m칩dulo con la funci칩n `get-url`, primero es necesario disponer de una estructura de carpetas y ficheros para la aplicaci칩n. Para comenzar, creamos una carpeta principal para la aplicaci칩n, denominada `ElTiempo`. Dentro de esta carpeta creamos otra carpeta denominada `lib` donde alojaremos los ficheros de m칩dulos o librer칤as y dentro de esta carpeta creamos un nuevo fichero denominado `GetURL.rakumod` que contendr치 el c칩digo de un m칩dulo de `Raku`, concretamente el de la funci칩n `get-url` que vimos en la parte anterior, pero ligeramente adaptado. El c칩digo de `GetURL.rakumod` es el siguiente:

```raku
unit module GetURL;

sub get-url ($url) is export {

    grammar URL {

        token TOP              { <protocol> <host> <resource-path>? }
        token protocol         { 'https://' }
        token host             { <[ a..z 0..9 _ . - ]> ** 5..40 }
        token resource-path    { '/' <[ a..z 0..9 / ? = & _ . - ]> ** 1..255 }
    }

    unless my $m = URL.parse($url) { return 'Bad URL'; }

    my $remote-port  = 443;
    my $http-version = 'HTTP/1.0';

    use IO::Socket::Async::SSL;
    my $conn = await IO::Socket::Async::SSL.connect($m.<host>, $remote-port);
    $conn.print: "GET $m.<resource-path> $http-version\r\nHost: $m.<host>\r\n\r\n";

    my $result = '';

    react {
        whenever $conn -> $buffer {
            $result ~= $buffer;
        }
    }

    $conn.close;

    return $result;
}
```

El m칩dulo comienza con `unit module GetURL;`, donde `GetURL` es el nombre del m칩dulo. M치s abajo aparece la definici칩n de la funci칩n `get-url` incluyendo `is export` para que la funci칩n quede disponible despu칠s de cargar el m칩dulo `GetURL` desde el c칩digo principal.

El c칩digo principal de la aplicaci칩n se incluye en el fichero denominado `ElTiempo.raku` ubicado en la carpeta principal de la aplicaci칩n `ElTiempo` que hemos creado antes. La estructura de carpetas y ficheros de momento quedar치 as칤:

```
游늬 ElTiempo
  游늯 ElTiempo.raku
  游늬 lib
    游늯 GetURL.rakumod
```

El c칩digo del script principal de la aplicaci칩n `ElTiempo.raku`, de momento es el siguiente:

```raku
use lib 'lib';
use GetURL;

my $url = 'https://api.open-meteo.com/v1/forecast?latitude=40.4167&longitude=-3.7033&current_weather=true';

get-url($url).say;
```

Lo primero que le decimos a la aplicaci칩n es la ubicaci칩n de la carpeta donde encontraremos los m칩dulos o librer칤as mediante `use lib 'lib';`. Como esta carpeta se encuentra en la misma carpeta del script principal, se escribe el nombre de la carpeta tal cual entre comillas simples. Despu칠s, con `use GetURL;` indicamos el nombre del m칩dulo que necesitamos utilizar. El nombre del m칩dulo se corresponde con el prefijo del nombre del fichero del m칩dulo `GetURL.rakumod` ubicado en la carpeta `'lib'`. De esta forma, la funci칩n `get-url` que est치 dentro del fichero `GetURL.rakumod` quedar치 disponible en este script.

Ya tenemos una estructura b치sica y operativa de la aplicaci칩n. Desde la l칤nea de comandos vamos a la carpeta `ElTiempo` y ejecutamos la aplicaci칩n con:

```raku Eltiempo.raku```

Si todo es correcto aparecer치 una respuesta similar a la que vimos antes, cuando ejecutamos la funci칩n `get-url` directamente.

### Buscando ciudades y coordenadas

Adaptemos el c칩digo de `Eltiempo.raku` para descargar el c칩digo HTML de la web donde se encuentra la informaci칩n de ciudades y coordenadas cuya URL es `https://open-meteo.com/en/docs`, y guardemos el c칩digo HTML descargado en el fichero `open-meteo.html`:

```raku
use lib 'lib';
use GetURL;

my $url = 'https://open-meteo.com/en/docs';

my $url-content = get-url($url);

spurt 'open-meteo.html', $url-content;
```

Ejecutamos con:

```
raku Eltiempo.raku
```

Abrimos `open-meteo.html` con un editor de texto y sobre la l칤nea 118 encontramos el comienzo de la informaci칩n que buscamos:

```html
<option selected data-latitude="52.5235" data-longitude="13.4115" data-asl="34">Berlin</option>
<option data-latitude="48.8567" data-longitude="2.3510" data-asl="34">Paris</option>
...
```

En cada l칤nea aparece la ciudad entre las etiquetas HTML `option` `<option ... > Nombre de ciudad </option>`, y dentro de cada etiqueta `option` la latitud como valor del atributo `data-latitude="n칰mero indicando la latitud"` y la longitud como valor del atributo `data-longitude="n칰mero indicando la longitud"`. 

Si nos fijamos en la primera l칤nea de las que hemos encontrado, vemos que comienza con `<option selected data-latitude...` y el resto de l칤neas con `<option data-latitude...`, esto es, la primera l칤nea incluye el atributo `selected` entre `<option` y `data-latitude` y el resto de estas l칤neas no incluye este atributo. Este detalle es importante y es necesario tenerlo en cuenta.

Tambi칠n vemos en todas las l칤neas la existencia del atributo `data-asl`, pero no aporta nada.

### Obteniendo ciudades y coordenadas con una gram치tica

Sabemos que en el c칩digo HTML descargado existen l칤neas entre etiquetas `<option ... > ... </option>` que contienen una ciudad y sus coordenadas. Tambi칠n conocemos los elementos que tiene cada una de estas l칤neas. Pongamos estos elementos que pertenecen a una l칤nea en la gram치tica `OPTION-CITIES`, en el token `TOP`:

```raku
grammar OPTION-CITIES {

  token TOP {
    <option-open> <latitude> <longitude> <data-asl> <city> <option-close>
  }
    
}
```

Despu칠s del token `TOP` definimos cada uno de sus elementos en su propio token, comenzando con el inicio de la etiqueta `<option...`:

```raku
token option-open {

  \s* ['<option ' | '<option selected ']

}
```

El token `option-open` comienza en el inicio de una l칤nea. La expresi칩n regular `\s*` tiene en cuenta los espacios que hay al principio de la l칤nea, si existen. El t칠rmino `\s` representa un espacio y `*` significa ninguno, uno o m치s de uno de lo que hay a la izquierda. Por tanto indicar `\s*` al principio de una l칤nea sirve para detectar si la l칤nea no comienza con ning칰n espacio o si comienza con un n칰mero indeterminado de ellos. Esto es importante y es necesario para tener la precisi칩n adecuada al detectar los siguientes elementos de la l칤nea. 

Despu칠s busca si aparece el texto `<option` o `<option selected`. De esta forma detectamos la primera l칤nea que, recordemos, comienza con `<option selected data-latitude` y tambi칠n detectamos el resto de l칤neas que comienzan con `<option data-latitude`. Como vemos, las dos opciones figuran entre corchetes `['<option ' | '<option selected ']` separadas del operador l칩gico `OR` representado con una barra vertical o tuber칤a.

Seguimos con el siguiente token `<latitude>` formado por dos token m치s: `<latitude-name>` para el nombre y `<latitude-value>` para el valor:

```raku
token latitude       { <latitude-name> <latitude-value> }
token latitude-name  { 'data-latitude="' }
token latitude-value { <number> }
```

El token `latitude-name` contiene el texto en s칤 entre comillas simples seg칰n viene en el c칩digo HTML descargado incluyendo hasta el s칤mbolo igual y las dobles comillas.

El token `latitude-value` es el valor que realmente necesitamos; es un d칤gito que puede ser un n칰mero decimal con signo, definido en su token correspondiente a continuaci칩n justo debajo:

```raku
token number {

  \-?\d+ [\. \d+]?
  
}
```

Aqu칤 utilizamos una expresi칩n regular indicando que:
- Puede comenzar con un s칤mbolo menos o no `\-?`
- Seguido de un d칤gito o m치s de uno `\d+`
- Despu칠s indicamos la parte decimal, que es opcional, entre corchetes seguido de una interrogaci칩n. Dentro de los corchetes la parte decimal comienza con un punto `\.` y un d칤gito o m치s de uno `\d+`.

Como vemos, en el contexto de expresiones regulares, la barra invertida o backslash sirve para indicar s칤mbolos textuales como el punto o el gui칩n medio y tambi칠n para denotar un comod칤n para un d칤gito o n칰mero `\d`. La interrogaci칩n significa uno o ninguno de lo que hay a la izquierda y el s칤mbolo `+` significa uno o m치s de uno de lo que hay a la izquierda.

El token `longitude` y los tokens que lo componen son similares al del token `latitude`:

```raku
token longitude       { <longitude-name> <longitude-value> }
token longitude-name  { '" data-longitude="' }
token longitude-value { <number> }
```

Como vemos, reutilizamos el token `number` ya definido anteriormente.

El token `data-asl` es sencillo:

```raku
token data-asl {

  '" data-asl="'<number>'">'
  
}
```
Este token solo sirve para saber que este texto est치 ah칤, ocupando el espacio que hay entre lo que tiene a su izquierda y a su derecha, identificando de una forma muy precisa estas l칤neas.

Cuidado con las combinaciones m칰ltiples de comillas simples y dobles. Dentro de cada par de comillas simples va el texto tal cual incluyendo comillas dobles.

El token de la ciudad solo permitir치 letras min칰sculas y may칰sculas y espacios `\s` con un total de entre 3 y 40 caracteres:

```raku
token city { <[ a..z A..Z \s ]> ** 3..40 }
```

Por 칰ltimo, cerramos con el token `option-close`, literalmente el texto de cierre de la etiqueta HTML `option` entre comillas simples:

```raku
token option-close { '</option>' }
```

Ya tenemos la gram치tica lista, ahora crearemos un `hash` denominado `%cities-lat-lon` donde introduciremos en 칠l cada ciudad con sus coordenadas correspondientes por cada l칤nea que encaje con la gram치tica. Dentro del hash, cada clave ser치 una ciudad y el valor ser치n dos valores, uno para la latitud y otro para la longitud:

```raku
my %cities-lat-lon = gather for $url-content.lines -> $line {

  next unless my $match = OPTION-CITIES.parse($line);

  take $match.<city> => {
    'latitude'  => $match.<latitude><latitude-value>,
    'longitude' => $match.<longitude><longitude-value>
  }
}
```

Construimos este hash directamente con el uso de `gather` y `take`, de forma que `gather` *recoge* lo que viene del `for`, que son las l칤neas del c칩digo HTML descargado `$url-content` con el m칠todo `.lines` dejando cada una `->` en la variable `$line`. Entre las llaves se ejecuta el c칩digo por cada l칤nea `$line` del c칩digo HTML descargado.

Este c칩digo comienza con

```raku
next unless my $match = OPTION-CITIES.parse($line);
```

simplemente ignora la l칤nea actual y salta `next` a la siguiente l칤nea si la gram치tica no encaja `unless` con la l칤nea actual `OPTION-CITIES.parse($line)`. Si encaja, los elementos de la gram치tica se quedar치n en la variable `$match`.

En este punto tenemos en `$match` los elementos de los que se compone cada l칤nea y que definimos en la gram치tica con los token correspondientes. Para obtener la ciudad utilizamos `$match.<city>` y para obtener la latitud de la ciudad utilizamos `$match.<latitude><latitude-value>`. Como vemos, la informaci칩n que necesitamos de cada l칤nea es accesible mediante el uso de los nombres de los distintos tokens.

Finalmente `take` devuelve el hash, formado aqu칤 mismo con los valores de los elementos obtenidos:

```raku
take $match.<city> => {
  'latitude'  => $match.<latitude><latitude-value>,
  'longitude' => $match.<longitude><longitude-value>
}
```

La clave de cada elemento del hash `%cities-lat-lon` es la ciudad `$match.<city>` mientras que el valor correspondiente se indica `=>` entre llaves. Dentro de estas llaves indicamos dos hashes m치s (sin nombre) entre una coma, donde el primero tiene como clave el texto `'latitude'` entre comillas simples y como valor el de la latitud obtenido de la gram치tica y el segundo de forma similar para la longitud. 

Por 칰ltimo, vamos a probar el funcionamiento del hash con la ciudad `Madrid`:

```raku
my $city = 'Madrid';
"  Ciudad: $city".say;
" Latitud: %cities-lat-lon{$city}<latitude>".say;
"Longitud: %cities-lat-lon{$city}<longitude>".say;
```

Si todo es correcto, ejecutamos la aplicaci칩n con `raku ElTiempo.raku` y veremos lo siguiente:

```
  Ciudad: Madrid
 Latitud: 40.4167
Longitud: -3.7033
```

Quedando el c칩digo principal en el fichero `ElTiempo.raku` de la siguiente forma:

```raku
use lib 'lib';
use GetURL;

my $url = 'https://open-meteo.com/en/docs';
my $url-content = get-url($url);

grammar OPTION-CITIES {

    token TOP {
        <option-open> <latitude> <longitude> <data-asl> <city> <option-close>
    }
    token option-open {
        \s* ['<option ' | '<option selected ']
    }
    token latitude       { <latitude-name> <latitude-value> }
    token latitude-name  { 'data-latitude="' }
    token latitude-value { <number> }
    token number {
        \-?\d+ [\. \d+]?
    }
    token longitude       { <longitude-name> <longitude-value> }
    token longitude-name  { '" data-longitude="' }
    token longitude-value { <number> }
    token data-asl {
        '" data-asl="'<number>'">'
    }
    token city { <[ a..z A..Z \s ]> ** 3..40 }
    token option-close { '</option>' }
}

my %cities-lat-lon = gather for $url-content.lines -> $line {

    next unless my $match = OPTION-CITIES.parse($line);

    take $match.<city> => {
        'latitude'  => $match.<latitude><latitude-value>,
        'longitude' => $match.<longitude><longitude-value>
    }
}

my $city = 'Madrid';
"  Ciudad: $city".say;
" Latitud: %cities-lat-lon{$city}<latitude>".say;
"Longitud: %cities-lat-lon{$city}<longitude>".say;
```
Hemos llegado al final de esta tercera parte donde hemos visto varias cosas interesantes:
* Creaci칩n de una estructura para una aplicaci칩n `Raku`
* Uso de m칩dulos y funciones
* Creaci칩n y uso de gram치ticas `Raku` para filtrar la informaci칩n descargada y obtener informaci칩n precisa
* Creaci칩n y uso de un hash para disponer de dicha informaci칩n

En la siguiente y 칰ltima parte ampliaremos estos conocimientos de `Raku` desarrollando un men칰 de ciudades, llevar a cabo los pasos del 3 al 7 y finalizar la aplicaci칩n.